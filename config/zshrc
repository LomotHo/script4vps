SCRIPT4VPS_HOME="${HOME}/.script4vps"
V="${SCRIPT4VPS_HOME}/vendor"

# OMZ cache directory (needed by some plugins)
ZSH_CACHE_DIR="${HOME}/.cache/zsh"
[[ -d "$ZSH_CACHE_DIR/completions" ]] || mkdir -p "$ZSH_CACHE_DIR/completions"
fpath=("$ZSH_CACHE_DIR/completions" $fpath)

autoload -Uz is-at-least compinit
compinit -d "$ZSH_CACHE_DIR/.zcompdump"

# OMZ core libs (load all)
for f in "${V}"/omz/lib/*.zsh; do
  source "$f"
done

# OMZ plugins (from config file)
while IFS= read -r plugin || [[ -n "$plugin" ]]; do
  plugin="${plugin%%#*}"        # strip comments
  plugin="${plugin// /}"        # strip spaces
  [[ -z "$plugin" ]] && continue
  source "${V}/omz/plugins/${plugin}/${plugin}.plugin.zsh"
done < "${SCRIPT4VPS_HOME}/config/enabled_plugins.txt"

# Agent mode detection (CI / non-interactive)
if [[ -n "$npm_config_yes" ]] || [[ -n "$CI" ]] || [[ "$-" != *i* ]]; then
  export AGENT_MODE=true
else
  export AGENT_MODE=false
fi

if [[ "$AGENT_MODE" != "true" ]]; then
  # Syntax highlighting
  source "${V}/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"

  # Powerlevel10k theme
  source "${V}/powerlevel10k/powerlevel10k.zsh-theme"
  [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
fi

# Profile (aliases, PATH, env)
source "${SCRIPT4VPS_HOME}/config/profile"
